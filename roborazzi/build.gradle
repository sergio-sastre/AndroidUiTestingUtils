plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'sergio.sastre.uitesting.roborazzi'
    compileSdk 34

    defaultConfig {
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.15'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation project(':utils')
    implementation project(':robolectric')
    implementation project(':mapper-roborazzi')

    implementation("org.hamcrest:hamcrest:3.0")
    implementation("io.github.darkxanter:webp-imageio:0.3.3")
    implementation("androidx.activity:activity-compose:1.9.3") {
        because "the last one requires compose option 1.6.x"
    }

    api 'io.github.takahirom.roborazzi:roborazzi:1.44.0'
    api 'androidx.test.espresso:espresso-core:3.6.1'
}

//https://www.talentica.com/blogs/publish-your-android-library-on-jitpack-for-better-reachability/
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.sergio-sastre'
            artifactId = "roborazzi"
            version = '2.7.0'

            afterEvaluate {
                from components.release
            }
        }
    }
}
