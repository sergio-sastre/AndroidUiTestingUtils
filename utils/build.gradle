plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33
        versionCode 22
        versionName "2.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xjvm-default=all"]
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.7.0-alpha02'
    api 'com.google.android.material:material:1.9.0'
    api 'androidx.test:core:1.5.0'

    //noinspection GradleDependency
    api ('androidx.core:core-ktx:1.9.0') {
        because '1.10.0 causes troubles with Paparazzi'
    }
    api 'androidx.test.ext:junit:1.1.5'
    api 'androidx.test.espresso:espresso-core:3.5.1'
    //noinspection GradleDependency
    api ('androidx.compose.ui:ui-test-junit4:1.4.0') {
        because 'it maches the compose compiler version'
    }
    api 'androidx.fragment:fragment-ktx:1.5.7'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}

//https://www.talentica.com/blogs/publish-your-android-library-on-jitpack-for-better-reachability/
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.sergio-sastre'
            artifactId = "utils"
            version = '2.0.1'

            afterEvaluate {
                from components.release
            }
        }
    }
}
