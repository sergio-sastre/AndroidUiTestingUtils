plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace = 'sergio.sastre.uitesting.utils'
    compileSdk 34

    defaultConfig {
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xjvm-default=all"]
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.6'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.7.0-alpha03'
    api 'com.google.android.material:material:1.11.0'
    api 'androidx.test:core:1.5.0'

    //noinspection GradleDependency
    api ('androidx.core:core-ktx:1.9.0') {
        because '1.10.0 causes troubles with Paparazzi'
    }
    api 'androidx.test.ext:junit:1.1.5'
    api 'androidx.test.espresso:espresso-core:3.5.1'
    api 'androidx.compose.ui:ui-test-junit4:1.5.4'
    api 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}

//https://www.talentica.com/blogs/publish-your-android-library-on-jitpack-for-better-reachability/
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.sergio-sastre'
            artifactId = "utils"
            version = '2.1.1-alpha02'

            afterEvaluate {
                from components.release
            }
        }
    }
}
